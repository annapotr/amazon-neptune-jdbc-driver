buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'java'
    id "com.github.spotbugs" version "4.5.1"
    id 'checkstyle'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'jacoco'
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
group 'software.amazon.neptune.jdbc'
version '1.0-SNAPSHOT'
description 'neptune-jdbc-driver'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
}

// Silently agree to build scan terms.
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

jar {
    manifest {
        attributes 'Implementation-Version': archiveVersion
    }
}

test {
    useJUnitPlatform()
    testLogging.showStandardStreams = true
    testLogging.events("passed", "skipped", "failed")
}

/**
 * CheckStyle Plugin
 */
apply plugin: 'checkstyle'
checkstyle {
    toolVersion '8.37'
    configFile file("config/checkstyle/checkstyle-rules.xml")
    ignoreFailures = false
}
checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

/**
 * SpotBugs Plugin
 */
spotbugs {
    showStackTraces = false
    reportsDir = file("$buildDir/reports/spotbugs")
    ignoreFailures = false
    includeFilter = file("config/spotbugs/spotbugs-exclude.xml")
}
spotbugsMain {
    // Configure HTML report
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/main.xml")
        }
    }
}
spotbugsTest {
    // Configure HTML report
    reports {
        xml {
            enabled = true
            destination = file("$buildDir/reports/spotbugs/test.xml")
        }
    }
}

task checkSpotBugsMainReport {
    doLast {
        def xmlReport = spotbugsMain.reports.getByName("XML")
        def slurped = new XmlSlurper().parse(xmlReport.destination)
        def bugsFound = slurped.BugInstance.size()
        slurped.BugInstance.each {
            println "SpotBugs Error"
            println "\tShort Message:\t\t${it.ShortMessage}"
            println "\tLong Message:\t\t${it.LongMessage}"
            println "\tSource of Error:\t${it.SourceLine.Message}"
        }
        if (bugsFound > 0) {
            throw new Exception("Encountered SpotBugs errors, see above.")
        }
    }
}

task checkSpotBugsTestReport {
    doLast {
        def xmlReport = spotbugsTest.reports.getByName("XML")
        def slurped = new XmlSlurper().parse(xmlReport.destination)
        def bugsFound = slurped.BugInstance.size()
        slurped.BugInstance.each {
            println "SpotBugs Error"
            println "\tShort Message:\t\t${it.ShortMessage}"
            println "\tLong Message:\t\t${it.LongMessage}"
            println "\tSource of Error:\t${it.SourceLine.Message}"
        }
        if (bugsFound > 0) {
            throw new Exception("Encountered SpotBugs errors, see above.")
        }
    }
}

spotbugsMain.finalizedBy checkSpotBugsMainReport
spotbugsTest.finalizedBy checkSpotBugsTestReport

/**
 * JaCoCo Plugin
 */
jacoco {
    toolVersion = "0.8.5"
}
jacocoTestReport {
    reports {
        html.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
test.finalizedBy(project.tasks.jacocoTestReport)
jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            excludes = [
            ]
            limit {
                counter = 'LINE'
                minimum = 0.00
            }
            limit {
                counter = 'BRANCH'
                minimum = 0.00
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it)
        }))
    }
}
check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn jacocoTestReport

/**
 * License Plugin
 */
license {
    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    headerDefinitions {
        slash_star_with_space {
            firstLine = '/*'
            endLine   = ' *\n */\n'
            beforeEachLine = ' * '
            firstLineDetectionPattern = '/\\*'
            lastLineDetectionPattern  = ' \\*\n \\*/\n'
        }
    }
    mapping {
        java = 'slash_star_with_space'
    }
}

repositories {
    mavenCentral()
    flatDir {
        dir 'amazon-neptune-tools/neptune-export/target'
        dir 'amazon-neptune-tools/neptune-gremlin-client/gremlin-client/target'
    }
}

ext {
    log4jVersion = '2.13.3'
    lombokVersion = '1.18.16'
    jupiterVersion = '5.4.0'
    neo4jDBVersion = '3.5.19'
    neo4jDriverVersion = '4.0.0'
    puppyCrawlVersion = '8.8'
    findBugsVersion = '3.0.1'
    awsSdkVersion = '1.11.932'
    gremlinVersion = '3.4.8'
    slf4jVersion = '1.7.29'
    rdf4jVersion = '3.5.0'
    amazonSigv4SignerVersion = '2.1.1'
    nettyVersion = '4.1.52.Final'
}

subprojects {
    tasks.withType(Test) {
        // Gradle documentation suggests using this number of cores.
        maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    }
}

dependencies {
    // Local neptune-export library
    implementation('com.amazonaws:neptune-export:1.0-SNAPSHOT')
    implementation(':neptune-export:1.0-SNAPSHOT')
    implementation('com.amazonaws:gremlin-client:1.0-SNAPSHOT')
    implementation(':gremlin-client:1.0-SNAPSHOT')

    // neptune-export dependencies
    compile group: 'com.github.rvesse', name: 'airline', version: '2.8.0'
    compile group: 'org.apache.tinkerpop', name: 'gremlin-driver', version: gremlinVersion
    compile group: 'org.eclipse.rdf4j', name: 'rdf4j-repository-sparql', version: rdf4jVersion
    compile group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-turtle', version: rdf4jVersion
    compile group: 'org.eclipse.rdf4j', name: 'rdf4j-rio-nquads', version: rdf4jVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-kinesis', version: awsSdkVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-neptune', version: awsSdkVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-s3', version: awsSdkVersion
    compile group: 'com.amazonaws', name: 'aws-java-sdk-core', version: awsSdkVersion
    compile group: 'com.amazonaws', name: 'amazon-neptune-sparql-java-sigv4', version: amazonSigv4SignerVersion
    compile group: 'com.amazonaws', name: 'amazon-neptune-sigv4-signer', version: amazonSigv4SignerVersion
    compile group: 'com.amazonaws', name: 'amazon-neptune-gremlin-java-sigv4', version: amazonSigv4SignerVersion

    // Might need tinkerpop, also shade exclude for testing???
    // compile group: 'org.apache.tinkerpop', name: 'gremlin-groovy', version: gremlinVersion
    // implementation group: 'org.fusesource.jansi', name: 'jansi', version: '2.0.0'

    compile group: 'io.netty', name: 'netty-all', version: nettyVersion
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.10.5'
    compile group: 'com.github.rvesse', name: 'airline', version: '2.8.0'
    compile group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
    compile group: 'org.apache.commons', name: 'commons-csv', version: '1.6'
    compile group: 'org.codehaus.plexus', name: 'plexus-utils', version: '3.2.0'
    compile group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-cbor', version: '2.10.5'

    // Creating IAM SigV4 Requests
    compile group: 'com.amazonaws', name: 'amazon-neptune-gremlin-java-sigv4', version: '1.0'

    // Logging
    implementation group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion

    // Utilities
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // Code analysis
    implementation group: 'com.puppycrawl.tools', name: 'checkstyle', version: puppyCrawlVersion
    compile group: 'com.google.code.findbugs', name: 'annotations', version: findBugsVersion
    testImplementation group: 'com.puppycrawl.tools', name: 'checkstyle', version: puppyCrawlVersion
    testCompile group: 'com.google.code.findbugs', name: 'annotations', version: findBugsVersion

    // Testing
    testImplementation  group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.1.0'
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // OpenCypher related
    implementation group: 'org.neo4j.driver', name: 'neo4j-java-driver', version: neo4jDriverVersion
    testCompile group: 'org.neo4j.test', name: 'neo4j-harness', version: neo4jDBVersion
    testCompile group: 'org.neo4j', name: 'neo4j-common', version: neo4jDBVersion, classifier: "tests"
    testCompile group: 'org.neo4j.community', name: 'it-test-support', version: neo4jDBVersion
    testCompile group: 'org.neo4j', name: 'neo4j-kernel', version: neo4jDBVersion, classifier: "tests"
    testCompile group: 'org.neo4j', name: 'neo4j-io', version: neo4jDBVersion, classifier: "tests"

    // Yaml file parsing
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.0'
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: '2.12.0'

    // https://mvnrepository.com/artifact/org.apache.jena/jena-arq
    implementation group: 'org.apache.jena', name: 'jena-arq', version: '3.17.0'

    // https://mvnrepository.com/artifact/org.apache.jena/jena-fuseki-main
    testImplementation group: 'org.apache.jena', name: 'jena-fuseki-main', version: '3.17.0'

}

shadowJar {
    baseName = rootProject.name
    classifier = ''

    // todo: exclude
    dependencies {
        exclude(dependency('org.slf4j:.*'))
        // exclude(dependency('org.apache.logging.log4j:.*:.*'))
    }
}